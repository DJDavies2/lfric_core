!-----------------------------------------------------------------------------
! (c) Crown copyright 2020 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!-----------------------------------------------------------------------------
!> @brief An algorithm for performing 1D-vertical-semi-Lagrangian scheme.
!> @details The algorithm calculates the departure of theta-points
!>          using only the vertical wind, then computes the theta at
!>          the departure points using a cubic interpolation.
!-----------------------------------------------------------------------------
module vert_sl_theta_alg_mod

  use constants_mod,                     only: r_def
  use io_config_mod,                     only: subroutine_timers
  use timer_mod,                         only: timer
  use field_mod,                         only: field_type
  use calc_departure_wind_kernel_mod,    only: calc_departure_wind_kernel_type
  use vertical_sl_theta_kernel_mod,      only: vertical_sl_theta_kernel_type
  use runtime_constants_mod,             only: get_coordinates

  implicit none

  private
  public :: vert_sl_theta

contains

  !-----------------------------------------------------------------------------
  !> @brief An algorithm to compute vertical departure points and interpolate
  !>        data to departure points (SL-advection).
  !> @param[in]      wind_adv   Wind used for SL-advection
  !> @param[in,out]  theta      Theta field at timestep n -> theta at departure
  !> @param[in]      dts        Time step (local)
  !-----------------------------------------------------------------------------
  subroutine vert_sl_theta( wind_adv, theta, dts )

    implicit none


    type( field_type ), intent(in)    :: wind_adv
    type( field_type ), intent(inout) :: theta
    real(kind=r_def), intent(in)      :: dts

    type( field_type ) :: departure_wind
    type( field_type ), pointer :: chi(:) => null()

    chi  => get_coordinates()
    call departure_wind%initialise( vector_space = wind_adv%get_function_space())

    if ( subroutine_timers ) call timer( 'vert_sl_theta' )

    ! set up the wind and compute SL advection (vertical only) of theta

    call invoke( name = "Calculate dept and interpolate",                        &
                 setval_c( departure_wind, 0.0_r_def ),                          &
                 calc_departure_wind_kernel_type(departure_wind, wind_adv, chi), &
                 vertical_sl_theta_kernel_type( departure_wind, theta, dts)      )

    nullify( chi )

    if ( subroutine_timers ) call timer( 'vert_sl_theta' )

  end subroutine vert_sl_theta

end module vert_sl_theta_alg_mod
